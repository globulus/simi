import "io" for *

DEFAULT_MD = "default.md"

fn frontmatter(title, visible = true) = "---
title: '$title docs'
visible: $visible
anchors:
   active: true
---

"

extend String {
    fn capitalizeFirst = @0.upperCased() + substringFrom(1)
    fn decapitalizeFirst = @0.lowerCased() + substringFrom(1)
    fn dashesToCamelCase = split("-").map(=$0.capitalizeFirst()).joinToString("").decapitalizeFirst()
    fn lastPathComponent = split("/").(-1)
}

fn ios() {
    SOURCE_DIR = "/Users/gordanglavas/Desktop/github/CodeCons/able-ios/.build/documentation/"
    TARGET_DIR = "/Users/gordanglavas/Desktop/github/CodeCons/able-fe/grav-admin/user/pages/02.devs/02.docs/01.ios"
    HOME_MD = "Home.md"
    merge = String.builder().add(File(SOURCE_DIR + HOME_MD).readString())
    for path in File(SOURCE_DIR).list()\
                .where(=$0 not in [HOME_MD, DEFAULT_MD, "_Footer.md", "_Sidebar.md"] and $0.endsWith(".md"))\
                .sorted() {
        merge.add("\n\n").add(File(SOURCE_DIR + path).readString())
    }
    content = merge.build().replacing("â€‹", "").replacing("x-source-tag://", "#").replacing("](/", "](#")
    WriteStream(File("$TARGET_DIR/$DEFAULT_MD")).use({
        $0.write(frontmatter("iOS") + content)
        print "Done writing to $($0.file.path)"
    })
}

fn android() {
    SOURCE_DIR = "/Users/gordanglavas/Desktop/github/CodeCons/able-android/ablelib/build/docs/ablelib"
    TARGET_DIR = "/Users/gordanglavas/Desktop/github/CodeCons/able-fe/grav-admin/user/pages/02.devs/02.docs/02.android"
    INDEX_MD = "index.md"
    
    fn readFile(file) {
        text = file.readLines()\
            .where(=not $0.startsWith("//"))\
            .joinToString("\n")
        allMatches = text.findAllRegex("(\\(|\\/)[a-z,A-Z,0-9,-]*\\.(md)").map(=$0.value).distinct()
        for match in allMatches {
            text = text.replacing(match, if "index.md" in match "" else\
                        match.dashesToCamelCase()\
                            .replacing("/", "#")\
                            .replacing("(", "(#")\
                            .replacing(".md", "")\
                    )
        }
        return text
    }

    fn scanDirectory(relativePath) {
        isRoot = relativePath.isEmpty()
        path = "$SOURCE_DIR/$relativePath"
        merge = String.builder().add(readFile(File("$path/$INDEX_MD")))
        for listed in File(path).list()\
                    .where(=$0 not in [INDEX_MD, "package-list"] and not $0.startsWith("."))\
                    .sorted() {
            file = File("$path/$listed")
            if file.isDirectory() {
                scanDirectory((if isRoot "" else "$relativePath/") + listed)
            } else {
                merge.add("\n\n").add(readFile(file))
            }
        }
        WriteStream(File("$TARGET_DIR/$relativePath/$DEFAULT_MD")).use({
            $0.write(frontmatter(if isRoot "Android" else relativePath.lastPathComponent(), isRoot) + merge.build())
            print "Done writing to $($0.file.path)"
        })
    }

    scanDirectory("")
}

#ios()
android()